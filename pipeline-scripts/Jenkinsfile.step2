#!/usr/bin/env groovy
import groovy.json.JsonSlurper
import hudson.FilePath

node (label: 'docker-enabled-agent') {
    //Clone example project from GitHub repository
    git url: 'https://github.com/jfrog-aparicio/p9-jfrog-webinar.git', branch: 'master'
    def jobName = env.JOB_NAME
    def SERVER_URL = "https://p9demo-artifactory.jfrog.team/artifactory"
    def REPO = "docker"
    def PROMOTE_REPO = "docker-prod-local"
    def SOURCE_REPO = "docker-stage-local"
    def rtServer = Artifactory.newServer url: SERVER_URL, credentialsId: CREDENTIALS
    def buildInfo = Artifactory.newBuildInfo()
    def tagName
    def artdocker_registry = "p9demo-artifactory.jfrog.team"
    buildInfo.env.capture = true

    //Fetch all depensencies from Artifactory
    stage('Dependencies') {
        currentBuild.displayName = BUILD_NUMBER
        dir('docker-framework') {
            try {
                println "Gather Java and Tomcat"
                def downloadSpec = """{
                         "files": [
                          {
                           "pattern": "tomcat-local/java/jdk-8u91-linux-x64.tar.gz",
                           "target": "jdk/jdk-8-linux-x64.tar.gz",
                           "flat":"true"
                          },
                          {
                           "pattern": "tomcat-local/org/apache/apache-tomcat/apache-tomcat-8.0.32.tar.gz",
                           "target": "tomcat/apache-tomcat-8.tar.gz",
                           "flat":"true"
                          }
                          ]
                        }"""

                rtServer.download (downloadSpec, buildInfo)
                if (fileExists('jdk/jdk-8-linux-x64.tar.gz') && fileExists('tomcat/apache-tomcat-8.tar.gz')) {
                    println "Downloaded dependencies"
                } else {
                    println "Missing Dependencies either jdk or tomcat - see listing below:"
                    sh 'ls -d */*'
                    throw new FileNotFoundException("Missing Dependencies")
                }
            } catch (Exception e) {
                println "Caught exception during resolution.  Message ${e.message}"
                throw e
            }
        }
    }
    //Build docker image named "docker-framework" with Java 8 and Tomcat
    stage('Build') {
        try {
          dir ('docker-framework') {
            withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: CREDENTIALS, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD']]) {
                    def rtDocker = Artifactory.docker server: rtServer
                    tagName = "${artdocker_registry}/docker-framework:${env.BUILD_NUMBER}"
                    docker.withRegistry ("https://${artdocker_registry}", CREDENTIALS) {
                         sh "docker login -u ${USERNAME} -p ${PASSWORD} http://${artdocker_registry}"
                         docker.build(tagName, "--build-arg REGISTRY=${artdocker_registry} .")
                    }
                    buildInfo = rtDocker.push(tagName, REPO, buildInfo)
                    rtServer.publishBuildInfo buildInfo
            }
          }
        } catch (ConcurrentModificationException CME) {
            println "Caught ConcurrentModificationException and ignore..."
        } catch (Exception e) {
          println "Build failed with exception: $e"
          throw e
        }
    }
    //Test docker image
    stage('Test') {
        dir('docker-framework/framework-test') {

            def gradleWarDownload = """{
            "files": [
                {
                  "pattern": "gradle-release-local/org/jfrog/example/gradle/webservice/1.1.2/*.war",
                  "target": "war/webservice.war",
                  "props": "unit-test=pass",
                  "flat": "true"
                }
              ]
            }"""

            rtServer.download(gradleWarDownload)
            updateDockerFile()
            def tagDockerFramework = "${artdocker_registry}/docker-framework-test:${env.BUILD_NUMBER}"
            docker.build(tagDockerFramework, "--build-arg REGISTRY=${artdocker_registry} .")
            if (testFramework(tagDockerFramework)) {
                println "Setting property and promotion"
                updateProperty ("functional-test=pass", SERVER_URL, SOURCE_REPO )
                sh "docker rmi ${tagName}"
            } else {
                updateProperty ("functional-test=fail; failed-test=page-not-loaded", SERVER_URL, SOURCE_REPO)
                currentBuild.result = 'UNSTABLE'
                sh "docker rmi ${tagName}"
                return
            }
        }
    }
    //Scan build's Artifacts in Xray
    stage('Xray Scan') {
        if (XRAY_SCAN == "YES") {
            def xrayConfig = [
                'buildName'     : env.JOB_NAME,
                'buildNumber'   : env.BUILD_NUMBER,
                'failBuild'     : FAIL_BUILD == "true"
            ]
            def xrayResults = rtServer.xrayScan xrayConfig
            echo xrayResults as String
            sleep 5
        } else {
            println "No Xray scan performed. To enable set XRAY_SCAN = YES"
        }
    }

    //Promote image from local staging repositoy to production repository
    stage ('Promote') {

        dir ('docker-framework') {
            def promotionConfig = [
              'buildName'          : env.JOB_NAME,
              'buildNumber'        : env.BUILD_NUMBER,
              'targetRepo'         : PROMOTE_REPO,
              'comment'            : 'Framework test with latest version of application',
              'sourceRepo'         : SOURCE_REPO,
              'status'             : 'Released',
              'includeDependencies': false,
              'copy'               : true,
              'failFast': false
            ]
            promoteBuild (SOURCE_REPO, PROMOTE_REPO, SERVER_URL)
            // rtServer.promote promotionConfig - occassionally will hang on dry run

            copyImage(SOURCE_REPO, PROMOTE_REPO, SERVER_URL, )
            reTagLatest (SOURCE_REPO, PROMOTE_REPO, SOURCE_REPO, SERVER_URL)
            reTagLatest (PROMOTE_REPO, PROMOTE_REPO, SOURCE_REPO, SERVER_URL)
         }
    }
}

def copyImage (source_repo, target_repo, server_url) {
    def BUILD_NUMBER = env.BUILD_NUMBER
    withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: CREDENTIALS, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD']]) {
        def curlString = "curl -u " + env.USERNAME + ":" + env.PASSWORD + " " + server_url
        def regTagStr = curlString +  "/api/docker/${source_repo}/v2/promote -X POST -H 'Content-Type: application/json' -d \
            '{ \
                \"targetRepo\" : \"${target_repo}\", \
                \"dockerRepository\" : \"docker-framework\", \
                \"tag\" : \"${BUILD_NUMBER}\", \
                \"copy\": true \
            }'"
        println "Curl String is " + regTagStr
        sh regTagStr
    }
}

def updateDockerFile () {
    def BUILD_NUMBER = env.BUILD_NUMBER
    sh 'sed -i "s/docker-framework:latest/docker-framework:$BUILD_NUMBER/" Dockerfile'
}

def reTagLatest (targetRepo, promote_repo, source_repo, server_url) {
     def BUILD_NUMBER = env.BUILD_NUMBER
     sh 'sed -E "s/@/$BUILD_NUMBER/" retag.json > retag_out.json'
     switch (targetRepo) {
          case promote_repo :
              println "Tagging " + promote_repo + " with latest"
              sh ("""
                sed -E "s/TARGETREPO/${promote_repo}/g" retag_out.json > retaga_out.json
                """)
              break
          case source_repo :
               println "Tagging " + source_repo + " with latest"
               sh ("""
                 sed -E "s/TARGETREPO/${source_repo}/" retag_out.json > retaga_out.json
                """)
               break
      }
      sh 'cat retaga_out.json'
      withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: CREDENTIALS, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD']]) {
          def curlString = "curl -u " + env.USERNAME + ":" + env.PASSWORD + " " + server_url
          def regTagStr = curlString +  "/api/docker/$targetRepo/v2/promote -X POST -H 'Content-Type: application/json' -T retaga_out.json"
          println "Curl String is " + regTagStr
          sh regTagStr
      }
}
//test docker image by runnning container
def testFramework (tag) {
    def result = false
    def stdout = ""
    docker.image(tag).withRun('--name test-qa -p 8181:8181') {c ->
        println "Test if app responds as expected."
        for (int i = 0; i < 6; ++i) {
            sleep 10
            stdout = sh(script: 'docker exec test-qa curl "http://localhost:8181/"', returnStdout: true)
            if (stdout.contains("Welcome")) {
                println "*** Passed Test: " + stdout
                result = true
                break
            } else {
                println "*** App did not respond as expected: " + stdout
            }
        }
        if (!result)
            println "*** Failed Test: " + stdout
    }

    sh "docker rmi ${tag}"
    return result
}

def updateProperty (property, artserverUrl, sourceRepo) {
    withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: CREDENTIALS, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD']]) {
            def curlString = "curl -u " + env.USERNAME + ":" + env.PASSWORD + " " + "-X PUT " + artserverUrl
            def updatePropStr = curlString +  "/api/storage/${sourceRepo}/docker-framework/${env.BUILD_NUMBER}?properties=${property}"
            println "Curl String is " + updatePropStr
            sh updatePropStr
     }
}

def promoteBuild (source_repo, promote_repo, SERVER_URL) {

    def buildPromotion = """ {
        "status"      : "Released",
        "comment"     : "Framework test with latest version of application",
        "ciUser"      : "jenkins",
        "sourceRepo"  : "${source_repo}",
        "targetRepo"  : "${promote_repo}",
        "copy"        : true,
        "dependencies" : false,
        "failFast": false
    }"""

    withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: CREDENTIALS, usernameVariable: 'USERNAME', passwordVariable: 'PASSWORD']]) {
        def createPromo = ["curl", "-X", "POST", "-H", "Content-Type: application/json", "-d", "${buildPromotion }", "-u", "${env.USERNAME}:${env.PASSWORD}", "${SERVER_URL}/api/build/promote/${env.JOB_NAME}/${env.BUILD_NUMBER}"]
        try {
           def getPromoResponse = createPromo.execute().text
           def jsonSlurper = new JsonSlurper()
           def promoStatus = jsonSlurper.parseText("${getPromoResponse}")
           if (promoStatus.error) {
               println "Promotion failed: " + promoStatus
           }
        } catch (Exception e) {
           println "Promotion failed: ${e.message}"
        }
    }
}